buildscript {
	ext {
		kotlinVersion = '1.3.30'
		kotlinCoroutinesVersion = '1.2.0'
		springVersion = '5.1.4.RELEASE'
		springBootVersion = '2.1.4.RELEASE'
		springfoxVersion = '3.0.0-SNAPSHOT'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
	}
}

def getVersionName() {
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags'
		standardOutput = stdout
	}
	def output = stdout.toString().trim()
	output = output.replaceFirst('v', '')
	output = output.replaceFirst(/([\d.]+)/, '$1')
	output = output.replaceFirst(/([\d.]+)-(\d+)-.*/, '$1-SNAPSHOT')
	return output
}

subprojects {
	group = 'io.aconite'
	version = getVersionName()

	apply plugin: 'idea'
	apply plugin: 'kotlin'
	sourceCompatibility = '1.8'

	repositories {
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
		maven { url 'https://repo.spring.io/snapshot' }
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
	}

	compileKotlin {
		kotlinOptions {
			freeCompilerArgs = [
					'-Xjsr305=strict',
					"-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
					"-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi",
					"-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"
			]
			jvmTarget = '1.8'
		}
	}

	compileTestKotlin {
		kotlinOptions {
			freeCompilerArgs = [
					'-Xjsr305=strict',
					"-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi",
					"-Xuse-experimental=kotlinx.coroutines.InternalCoroutinesApi",
					"-Xuse-experimental=kotlinx.coroutines.ObsoleteCoroutinesApi"
			]
			jvmTarget = '1.8'
		}
	}

	idea {
	    module {
	        downloadJavadoc = true
	        downloadSources = true
	    }
	}

	test {
	    testLogging.showStandardStreams = true
	}
}

task printVersion {
	doLast {
		println("version = ${getVersionName()}")
	}
}

wrapper {
	gradleVersion = '4.10'
}